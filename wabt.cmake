
include(ExternalProject)

set(WABT_SOURCE_DIR ${CMAKE_CURRENT_LIST_DIR}/wabt)
set(WABT_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/wabt.build)
set(WABT_INCLUDE_DIRS
    ${WABT_SOURCE_DIR}/include
    ${WABT_BINARY_DIR}/include
)

if (LINUX)
    set(START_GROUP "-Wl,--start-group")
    set(END_GROUP   "-Wl,--end-group")

    set(WHOLE_ARCHIVE "-Wl,--whole-archive")
    set(NO_WHOLE_ARCHIVE "-Wl,--no-whole-archive")
endif()

if (LINUX OR APPLE)
    set(WABT_STATIC_LIBRARIES
        ${WABT_BINARY_DIR}/libwabt.a
    )

elseif (WIN32)
    set(WABT_STATIC_LIBRARIES
        ${WABT_BINARY_DIR}/wabt.lib
    )

endif()

if (APPLE)
    list(APPEND CMAKE_EXTRAS
        -UCMAKE_OSX_ARCHITECTURES
        -DCMAKE_OSX_ARCHITECTURES=${CMAKE_OSX_ARCHITECTURES}
        -UCMAKE_OSX_DEPLOYMENT_TARGET
        -DCMAKE_OSX_DEPLOYMENT_TARGET=${CMAKE_OSX_DEPLOYMENT_TARGET}
    )
endif()

ExternalProject_Add(wabt
    SOURCE_DIR ${WABT_SOURCE_DIR}
    GIT_REPOSITORY "https://github.com/WebAssembly/wabt"
    GIT_TAG main
    GIT_PROGRESS TRUE
    CONFIGURE_COMMAND ${CMAKE_COMMAND}
        -G ${CMAKE_GENERATOR}
        -S ${WABT_SOURCE_DIR}
        -B ${WABT_BINARY_DIR}

        -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
        -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
        -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
        -DCMAKE_MAKE_PROGRAM=${CMAKE_MAKE_PROGRAM}
        -DCMAKE_POSITION_INDEPENDENT_CODE:BOOL=${CMAKE_POSITION_INDEPENDENT_CODE}
        -DCMAKE_PREFIX_PATH=${CMAKE_PREFIX_PATH}
        -DCMAKE_VERBOSE_MAKEFILE=${CMAKE_VERBOSE_MAKEFILE}
        -DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}

        ${CMAKE_EXTRAS}

    BUILD_COMMAND ${CMAKE_COMMAND}
        --build ${WABT_BINARY_DIR}

    BUILD_BYPRODUCTS ${WABT_STATIC_LIBRARIES}
    INSTALL_COMMAND ""

    USES_TERMINAL_CONFIGURE TRUE
    USES_TERMINAL_BUILD TRUE
)
